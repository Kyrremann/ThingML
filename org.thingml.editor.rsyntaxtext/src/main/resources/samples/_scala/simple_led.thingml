import "../../hardware/bricks/_scala/led.thingml"
import "../../hardware/bricks/_scala/button.thingml"
import "../../core/_scala/timer.thingml"

thing SimpleLed includes LedMsgs, ButtonMsgs, TimerMsgs
{
	readonly property frequency : UInt16 = 1000
	
    required port Button
    {
        receives press, release    
    }

	required port Led
	{
		sends led_toggle
	}
	
	required port Timer
	{
		sends timer_start, timer_cancel
		receives timer_timeout
	}
    
    required port Led2
	{
		sends led_toggle
	}
    
	required port Timer2
	{
		sends timer_start, timer_cancel
		receives timer_timeout
	}
	
	statechart SimpleLedImpl init Idle
	{
		state Idle
        {
           transition -> Running
           event Button?release
        }

        state Running
		{
			on entry Timer!timer_start(frequency)
			
			transition toggle -> Running
			event Timer?timer_timeout
			action Led!led_toggle()

            transition -> Idle
            event Button?release
            action Timer!timer_cancel()
		}

        region test init Idle {
		    state Idle
            {
                transition -> Running
                event Button?press
            }

            state Running
		    {
			     on entry Timer2!timer_start(frequency)
			
			     transition toggle -> Running
			     event Timer2?timer_timeout
			     action Led2!led_toggle()

                 transition -> Idle
                 event Button?press
                 action Timer2!timer_cancel()
		     }
            
        }
	}
}

configuration fragment ScalaSimpleLedSimulatorFragment
{		
	// Led configuration.
	instance led : LedScala
    instance led2 : LedScala
		
	// Timer configuration.
	instance timer : TimerScala
    instance timer2 : TimerScala

    //Button configuration
    instance button : ButtonScala
    connector button.Sensor => button.SensorMockUp
	
	// Simple led configuration.
	instance simple_led : SimpleLed
        set simple_led.frequency = 800
	connector simple_led.Led => led.Led
	connector simple_led.Timer => timer.timer
	connector simple_led.Led2 => led2.Led
	connector simple_led.Timer2 => timer2.timer
    connector simple_led.Button => button.Button
}

configuration ScalaSimpleLedSimulator
{
    group g1 : ScalaSimpleLedSimulatorFragment
    group g2 : ScalaSimpleLedSimulatorFragment
    group g3 : ScalaSimpleLedSimulatorFragment
    group g4 : ScalaSimpleLedSimulatorFragment
    group g5 : ScalaSimpleLedSimulatorFragment
    group g6 : ScalaSimpleLedSimulatorFragment
    group g7 : ScalaSimpleLedSimulatorFragment
    group g8 : ScalaSimpleLedSimulatorFragment
    group g9 : ScalaSimpleLedSimulatorFragment
    group g10 : ScalaSimpleLedSimulatorFragment
}