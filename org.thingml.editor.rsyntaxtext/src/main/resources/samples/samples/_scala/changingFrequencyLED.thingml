import "../../hardware/bricks/_scala/led.thingml"
import "../../hardware/bricks/_scala/rotation_sensor.thingml"

import "../../hardware/devices/_scala/servo.thingml"

import "../../core/_scala/timer.thingml"

thing ChangingFrequencyLed includes LedMsgs, TimerMsgs, RotationSensorMsgs, ServoMsgs
{
	property frequency : UInt16 = 500
	
	required port Led
	{
		sends led_toggle
	}

	required port Servo {
		sends setPosition
	}
			
	required port Potentiometer {
		receives rotation_value
	}

	required port Timer
	{
		sends timer_start
		receives timer_timeout
	}
		
	statechart behavior init Running
	{
		state Running
		{
			on entry Timer!timer_start (frequency)
			
			transition toggle -> Running
				event Timer?timer_timeout
				action Led!led_toggle ()
			
			transition update -> Running
				event e: Potentiometer?rotation_value
				action do
                    print("R1")
                    frequency = e.val
                end
		}

        region Servo init Running
        {
            state Running
		    {
             internal
				event e: Potentiometer?rotation_value
				action do
                    print("R2")
                    if (e.val > 180) do
                        Servo!setPosition(180)
                    end
                    if (e.val < 180) do
                        Servo!setPosition(e.val)
                    end
                end
            }
        }
	}
}

configuration ScalaChangingFrequencyLedSimulator
{
	instance led : LedScala
    instance servo : ServoScala
	instance potentiometer: RotationSensorScala
    connector potentiometer.Sensor => potentiometer.SensorMockUp
	instance timer : TimerScala
	instance controller: ChangingFrequencyLed
	
	connector controller.Timer => timer.timer 
	connector controller.Led => led.Led
    connector controller.Servo => servo.Servo
	connector controller.Potentiometer => potentiometer.RotationSensor
}